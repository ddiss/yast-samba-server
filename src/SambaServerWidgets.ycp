/**
 * File:	modules/SambaServerWidgets.ycp
 * Package:	Configuration of samba-server
 * Summary:	Widgets used by SAMBA server configuration
 * Authors:	Jiri Srain <jsrain@suse.cz>
 *		Stanislav Visnovsky <visnov@suse.cz>
 *
 * $Id$
 */

{
    module "SambaServerWidgets";

    textdomain "samba-server";

    import "Label";
    import "Popup";
    import "LogView";
    import "TablePopup";
    import "SambaServer";
    import "SambaServerPassdb";
    
    include "samba-server/routines.ycp";
    include "samba-server/helps.ycp";

    global define list ShareEditContents (map descr);
    global define void ShareEditPopupInit (any opt_id, string opt_key);
    global define void ShareEditPopupStore (any opt_id, string opt_key, map event);
    global define string ShareEditSummary (any opt_id, string opt_key);
    global define boolean ShareEditEntryDelete (any opt_id, string opt_key);

    global define list GlobalSettingsContents (map descr);
    global define void GlobalSettingsPopupInit (any opt_id, string opt_key);
    global define void GlobalSettingsPopupStore (any opt_id, string opt_key, map event);
    global define string GlobalSettingsSummary (any opt_id, string opt_key);
    global define boolean GlobalSettingsEntryDelete (any opt_id, string opt_key);

    global define list PassdbEditContents (map descr);
    global define string PassdbEditOptionSummary (any opt_id, any opt_key);
    global define boolean PassdbOptionMove (any opt_id, string opt_key, symbol direction);
    global define string PassdbId2Key (map desc, any id);
    global define boolean PassdbEntryDelete (any opt_id, any opt_key);
    global define void PassdbEditOptionInit (any opt_id, any opt_key);
    global define void PassdbEditOptionStore (any opt_id, any opt_key, map event);

/**
 * A share name do be editted in the Edit share dialog
 */
global string shareToEdit = nil;

global define boolean initFun(any key);
global define symbol handleFun(any key, map event_descr);
global define boolean initPassdb(any key);
global define symbol handlePassdb(any key, map event_descr);
global define void storePassdb(any key, map event);
global define void AddPassdbBackend ();

// translators: error message for wrong LDAP source
string wrong_ldap_url = _("The entered LDAP URL is invalid.");

boolean ValidateLDAPURL (any opt_id, any opt_key, map event_descr) {

    import "URL";
    
    string url = (string) UI::QueryWidget ( `id (opt_key), `Value );

    if ( ! URL::Check (url) )
    {
	Popup::Error ( wrong_ldap_url );
	return false;
    }
    
    map u = URL::Parse (url);
    if ( u["scheme"]:nil != "ldap" && u["scheme"]:nil != "ldaps" )
    {
	Popup::Error ( wrong_ldap_url );
	return false;
    }
    
    return true;
}

global boolean SharePathWarning (string p)
{
    string text = nil;

    if ( p == "/" )
    {
	text = root_warning;
    }
    else    
    foreach (string known, maplist (string key, string val, warnings, ``(key)), ``{
	if (find (p,known) == 0)
	{
	    text = warnings [known]:nil;
	}
    });
    
    if (text != nil)
    {
	return Popup::ContinueCancel (text);
    }
    
    return true;
}

boolean ValidateSharePath (any opt_id, any opt_key, map event_descr) {

    string p = (string)UI::QueryWidget (`id(opt_id), `Value);

    return SharePathWarning (p);
}


include "samba-server/samba-options-local.ycp";
include "samba-server/samba-options-global.ycp"; 

global map shareswidget =  TablePopup::CreateTableDescr (
	    $["add_delete_buttons" : true, 
	      "up_down_buttons" : false, 
	      "unique_keys" : true ]
	  , $[
	    "init" : initFun,
	    "handle" : handleFun,
	    "options" : local_option_widgets,
	    "ids" : SambaServerWidgets::ShareEditContents,
	    "fallback" : $[
		"init" : SambaServerWidgets::ShareEditPopupInit,
		"store" : SambaServerWidgets::ShareEditPopupStore,
		"summary" : SambaServerWidgets::ShareEditSummary,
	    ],
	    "option_delete" : SambaServerWidgets::ShareEditEntryDelete,
	    "add_items" : maplist( string key, map values, SambaServerWidgets::local_option_widgets, 
		``( key ) ),
	    "help" : HELPS["share_edit"]:"",
	]);

global map globalsettingswidget =  TablePopup::CreateTableDescr (
	    $["add_delete_buttons" : true, 
	      "up_down_buttons" : false, 
	      "unique_keys" : true]
	  , $[
	    "options" : global_option_widgets,
	    "ids" : SambaServerWidgets::GlobalSettingsContents,
	    "fallback" : $[
		"init" : SambaServerWidgets::GlobalSettingsPopupInit,
		"store" : SambaServerWidgets::GlobalSettingsPopupStore,
		"summary" : SambaServerWidgets::GlobalSettingsSummary,
	    ],
	    "option_delete" : SambaServerWidgets::GlobalSettingsEntryDelete,
	    "add_items" : maplist( string key, map values, global_option_widgets, 
		``( key ) ),
	    "help" : HELPS["global_settings"]:"",
	]);

global map passdboptions = $[
    "smbpasswd" : $[
        "table" : $[
	    // table entry description for smbpasswd-based SAM
           "label" : _("smbpasswd file"),
        ],
        "popup" : $[
         "widget" : `textentry,
        ],
    ],
    "ldapsam" : $[
        "table" : $[
	    // table entry description for LDAP-based SAM
           "label" : _("LDAP"),
        ],
        "popup" : $[
            "widget" : `textentry,
	    "validate_type" : `function,
	    "validate_function" : ValidateLDAPURL
        ],
    ],
    "tdbsam" : $[
        "table" : $[
	    // table entry description for TDB-based SAM
           "label" : _("TDB database"),
        ],
        "popup" : $[
         "widget" : `textentry,
        ],
    ],
    "mysql" : $[
        "table" : $[
	    // table entry description for MySQL-based SAM
           "label" : _("MySQL database"),
        ],
        "popup" : $[
         "widget" : `textentry,
        ],
    ],
];

global map passdbwidget =  TablePopup::CreateTableDescr (
	    $["add_delete_buttons" : true, 
	      "up_down_buttons" : true, 
	      "unique_keys" : false]
	  , $[
	    "init" : initPassdb,
	    "handle" : handlePassdb,
	    "store" : storePassdb,
	    "ids" : PassdbEditContents,
	    "help" : HELPS["passdb_edit"]:"",
	    "options": passdboptions,
	    "add_items" : [ "smbpasswd", "ldapsam", "tdbsam", "mysql" ],
	    "add_unlisted" : false,
	    "id2key": SambaServerWidgets::PassdbId2Key,
	    "option_delete" : SambaServerWidgets::PassdbEntryDelete,
	    "option_move" : SambaServerWidgets::PassdbOptionMove,
	    "fallback" : $[
		"init"	  : PassdbEditOptionInit,
		"store"	  : PassdbEditOptionStore,
		"summary" : PassdbEditOptionSummary,
	    ],
	]);


/**
 * Map of widgets for CWM
 */
global map widgets = $[
	"share_edit" : shareswidget,
	"passdb_edit" : passdbwidget,
	"globalsettings" : globalsettingswidget,
];

global define boolean initFun(any key) ``{
    return TablePopup::TableInit(SambaServerWidgets::widgets["share_edit"]:$[], (string)key);
}

global define symbol handleFun(any key, map event_descr) ``{
    return TablePopup::TableHandle(SambaServerWidgets::widgets["share_edit"]:$[], (string)key, event_descr);
}

// ************************************ default host table ********************

    /**
      * Function for getting contents of the default host table
      * @param descr map description map of the table
      * @return list of items for the table
      */
    global define list ShareEditContents (map descr) ``{
	return filter( string key, maplist ( string k, any v, SambaServer::shares[shareToEdit]:$[], 
	    ``((v == nil ||  k == "commentout" || k == "yast2") ? nil : k )),   ``( key != nil ) );
    }
    
    /**
      * Delete function of the global table
      * @param opt_id any option id of selected option
      * @param opt_key any option key of selected option
      * @return boolean true if was really deleted
      */
    global define boolean ShareEditEntryDelete (any opt_id, string opt_key) ``{
	// message popup
	if (! Popup::YesNo (_("Delete the selected entry?")))
	    return false;
//	SambaServer::shares[shareToEdit]
//	    = filter (string k, any v, SambaServer::shares[shareToEdit]:$[], ``(k != opt_id));
	SambaServer::shares[shareToEdit, opt_key] = nil;
	return true;
    }

    /**
      * Fallback initialization function of a table entry / popup
      * @param opt_id any option id
      * @param opt_key any option key
      */
    global define void ShareEditPopupInit (any opt_id, string opt_key) ``{
	if( opt_id != nil ) {
	    any value = SambaServer::shares[shareToEdit, opt_key]:nil;
	    
	    // not adding a new option
	    UI::ChangeWidget (`id (opt_key), `Value,
		value);
	}
	UI::SetFocus (`id (opt_key));
    }

    /**
      * Fallback store function of a table entry / popup
      * @param opt_id any option id
      * @param opt_key any option key
      */
    global define void ShareEditPopupStore (any opt_id, string opt_key, map event) ``{
	SambaServer::shares[shareToEdit, opt_key] = UI::QueryWidget (`id (opt_key), `Value);
	SambaServer::modified = true;
    }

    /**
      * Fallback summary function of a table entry / popup
      * @param opt_id any option id
      * @param opt_key any option key
      * @return	value of the option
      */
    global define string ShareEditSummary (any opt_id, string opt_key) ``{
	return sformat ("%1", SambaServer::shares[shareToEdit, opt_key]:(any)"");
    }


/********************************************************************************************/

    /**
      * Function for getting contents of the default host table
      * @param descr map description map of the table
      * @return list of items for the table
      */
    global define list GlobalSettingsContents (map descr) ``{
	list<string> keys = maplist ( string k, any v, SambaServer::global_config, ``(k) );
	keys = filter(string key, keys, {return SambaServer::global_config[key]:nil != nil; });
	return keys;
    }
    
    /**
      * Delete function of the global table
      * @param opt_id any option id of selected option
      * @param opt_key any option key of selected option
      * @return boolean true if was really deleted
      */
    global define boolean GlobalSettingsEntryDelete (any opt_id, string opt_key) ``{
	// message popup
	if (! Popup::YesNo (_("Delete the selected entry?")))
	    return false;
//	SambaServer::global_config
//	    = filter (string k, any v, SambaServer::global_config, ``(k != opt_id));
	SambaServer::global_config[opt_key] = nil;
	return true;
    }

    /**
      * Fallback initialization function of a table entry / popup
      * @param opt_id any option id
      * @param opt_key any option key
      */
    global define void GlobalSettingsPopupInit (any opt_id, string opt_key) ``{
	if( opt_id != nil ) {
	    any value = SambaServer::global_config[opt_key]:nil;
	    
	    // not adding a new option
	    UI::ChangeWidget (`id (opt_key), `Value,
		value);
	}
	UI::SetFocus (`id (opt_key));
    }

    /**
      * Fallback store function of a table entry / popup
      * @param opt_id any option id
      * @param opt_key any option key
      */
    global define void GlobalSettingsPopupStore (any opt_id, string opt_key, map event) ``{
	any val = UI::QueryWidget (`id (opt_key), `Value);
	if (val != SambaServer::global_config[opt_key]:nil) {
	    SambaServer::global_config[opt_key] = UI::QueryWidget (`id (opt_key), `Value);
	    SambaServer::modified = true;
	}
    }

    /**
      * Fallback summary function of a table entry / popup
      * @param opt_id any option id
      * @param opt_key any option key
      * @return	value of the option
      */
    global define string GlobalSettingsSummary (any opt_id, string opt_key) ``{
	return sformat ("%1", SambaServer::global_config[opt_key]:(any)"");
    }

/************************************* passdb list table ************************************/

    integer max_id = 0;
    map<string, string> passdb_backends = nil;
    list<string> passdb_order = [];

    global define list PassdbEditContents (map descr) ``{
	if (passdb_backends != nil)
	{
	    // we are already initialized
	    return passdb_order;
	}
	else 
	{
	    list<string> l = SambaServerPassdb::GetBackends ();
	    list<string> ids = [];
	    max_id = -1;
	    passdb_backends = (map<string, string>)listmap ( string backend, l, ``{ 
		max_id = max_id + 1;
		string s = sformat( "%1", max_id );
		ids = add( ids, s );
		return $[ s: backend ];
	    });
	    max_id = max_id + 1;
	    passdb_order = ids;
	    return ids;
	}
    }
    
    global define void PassdbEditOptionInit (any opt_id, any opt_key) ``{
	if( opt_id != nil ) {
	    // not adding a new option
	    UI::ChangeWidget (`id (opt_key), `Value,
		SambaServerPassdb::BackendDetails(passdb_backends [(string)opt_id]:""));
	}
	UI::SetFocus (`id (opt_key));
    }

    global define void PassdbEditOptionStore (any opt_id, any opt_key, map event) ``{
	string details = (string) UI::QueryWidget (`id (opt_key), `Value);
	// TODO: handle empty details
	
	if (size(details) != 0 )
	{
	    details = ":"+details;
	}
	
	if ( opt_id != nil )
	{
	    // update
	    // take the SAM type from the old data and append the new details
	    passdb_backends[ (string)opt_id ] = SambaServerPassdb::BackendSAM(passdb_backends[ (string)opt_id ]:nil)
		+ details;
	}
	else
	{
	    // insert new
	    string id = sformat ("%1", max_id);
	    max_id = max_id+1;
	    passdb_backends = (map<string, string>)add ( passdb_backends, id, (string)opt_key+details );
	    passdb_order = (list<string>) add (passdb_order, id);
	}
	
	SambaServer::modified = true;
    }

    global define string PassdbEditOptionSummary (any opt_id, any opt_key) ``{
	string option = passdb_backends [(string)opt_id]:"";
	return SambaServerPassdb::BackendDetails (option);
    }
    
    global define string PassdbId2Key (map desc, any id)
    {
	string s_id = passdb_backends [(string)id]:"";
	return SambaServerPassdb::BackendSAM (s_id);
    }

    global define boolean PassdbEntryDelete (any opt_id, any opt_key) ``{
    
	// don't delete the last one
	if (size (passdb_backends) == 1)
	{
	    import "Report";
	    
	    // error message if user tries to delete the last passdb backend
	    Report::Error (_("At least one back-end must be specified.

The back-end will not be deleted.
"));
	    return false;
	}
	
	// message popup
	if (! Popup::YesNo (_("Delete the selected back-end?")))
	    return false;
	    
	string id = (string) opt_id;
	passdb_backends = (map<string, string>)filter (string k, string v, passdb_backends, ``(k != id));
	passdb_order = (list<string>)filter (string k, passdb_order, ``(k != id));
	SambaServer::modified = true;
	return true;
    }
    
    global define void AddPassdbBackend () {
	UI::OpenDialog (
	    `VBox (
		// translators: frame text when adding a passdb backend
		`Frame (_("Back-end Type"),
		`RadioButtonGroup ( `id (`types),  
		`VBox(
		    // translators: passdb backend radio button
		    `Left (`RadioButton ( `id("smbpasswd"), `opt (`notify),_("smbpasswd file") ) ),
		    // translators: passdb backend radio button
		    `Left (`RadioButton ( `id("ldapsam"), `opt (`notify),_("LDAP") ) ),
		    // translators: passdb backend radio button
		    `Left (`RadioButton ( `id("tdbsam"), `opt (`notify),_("TDB database") ) ),
		    // translators: passdb backend radio button
		    `Left (`RadioButton ( `id("mysql"), `opt (`notify),_("MySQL database") ) )
		))),
		// translators: textentry label to enter details for the selected passdb backend
		`TextEntry ( `id(`details), _("&Details:") ),
		`HBox (
		    `PushButton (`id (`add), `opt(`default), Label::AddButton ()),
		    `PushButton (`id (`cancel), Label::CancelButton ())
		)
	    )
	);
	
	do {
	    any ret = UI::UserInput ();
	    if ( contains ( (list)[ "smbpasswd", "ldapsam", "tdbsam", "mysql" ], ret ) )
	    {
		// prefill a hint, if details empty
		string details = (string) UI::QueryWidget ( `details, `Value );
		if (size(details) == 0)
		{
		    if ( ret == "ldapsam" )
		    {
			import "Ldap";
			// ask LDAP client for the value
			string server = Ldap::server;
			
			// ensure to take only the first one
			list<string> servers = splitstring (server, " ");
			UI::ChangeWidget ( `id (`details), `Value, "ldap://" + servers[0]:"localhost" );
		    }
		}
		continue;
	    } else
	
	    if ( ret == `add )
	    {
		// check: all types except smbpasswd must get detailed info
		string type = (string) UI::QueryWidget ( `types, `CurrentButton );
		string details = (string) UI::QueryWidget ( `details, `Value );
		if (size(details) != 0 )
		{
		    details = ":"+details;
		}
	
		if (type == "mysql" && size (details) == 0)
		{
		    // translators: error message, if the MySQL backend 
		    // is selected, but no details are entered
		    Popup::Error ( _("An identifier must be provided
in details 
for the MySQL passdb back-end.

Consult the Samba HOWTO Collection for
further information.
") );
		    continue;
		}
		
		// validate LDAP
		else if (type == "ldapsam" && size(details) > 0)
		{
		    // fill in empty params to keep interpreter happy
		    if (!ValidateLDAPURL (nil, `details,nil) )
		    {
			continue;
		    }
		}
		
		// add the value		
		string id = sformat ("%1", max_id);
		max_id = max_id+1;
		passdb_backends = (map<string, string>)add ( passdb_backends, id, type+details );
		passdb_order = (list<string>) add (passdb_order, id);
		SambaServer::modified = true;
	    }
	    break;
	} while (true);
	
	UI::CloseDialog ();
    }
    
    global define boolean PassdbOptionMove (any opt_id, string opt_key, symbol direction) {
	string id = (string)opt_id;
	passdb_order = sort (string left, string right, passdb_order, ``{ boolean res = 
	    direction == `down ?
	    (left == id ? false : true)
	    :
	    (right == id ? false : true);
	    return res;
	});
	initPassdb (opt_key);
	SambaServer::modified = true;
	return true;
    }

    // test the LDAP over SSL connection
    define void testLDAPServer (string server, string suffix) {
        // translators: progress dialog text
        UI::OpenDialog(`Label( _("Testing LDAP connection...") ) );
        if( SambaServer::testLDAPS( server, suffix ) )
        {
            // translators: test result popup message
            Popup::Message( _("LDAP connection is OK.") );
        }
        UI::CloseDialog();
    }


    global define boolean initPassdb(any key) ``{
	return TablePopup::TableInit(SambaServerWidgets::widgets["passdb_edit"]:$[], (string)key);
    }

    global define symbol handlePassdb(any key, map event_descr) ``{
	if ( event_descr["ID"]:nil == `_tp_add )
	{
	    AddPassdbBackend ();
	    TablePopup::TableInit(SambaServerWidgets::widgets["passdb_edit"]:$[], (string)key);
	    // continue
	    return nil;
	}

	// LDAP settings menu item selected, move to a different dialog	
	if ( event_descr["ID"]:nil == `ldap )
	{
	    return `ldap;
	}
	
	// test LDAP menu item selected, try the item
	if ( event_descr["ID"]:nil == `ldap_test )
	{
	    string backend = passdb_backends[(string)UI::QueryWidget (`_tp_table, `CurrentItem)]: nil;
	    
	    if (SambaServerPassdb::BackendSAM(backend) != "ldapsam")
	    {
		// translators: the current item is not LDAP, LDAP connection cannot be tested.
		Popup::Error (_("Select an LDAP back-end to test."));
		return nil;
	    }
	    testLDAPServer( SambaServerPassdb::BackendDetails(backend), SambaServer::LDAP_suffix);
	    return nil;	    
	}
	
	if ( event_descr["ID"]:nil == `back )
	{
	    passdb_backends = nil;
	    return nil;
	}
    
	return TablePopup::TableHandle(SambaServerWidgets::widgets["passdb_edit"]:$[], (string)key, event_descr);
    }

    global define void storePassdb(any key, map event) ``{
	list<string> res = maplist (string id, passdb_order, ``{
	    return passdb_backends[id]:"";
	});
	SambaServerPassdb::SetBackends (res);
	passdb_backends = nil; // require a new initialization
	if (SambaServer::role == `pdc )
	{
	    SambaServer::AdaptMachineScript ();
	}
	
	SambaServer::global_config ["passdb backend"] = mergestring (SambaServerPassdb::GetBackends (), " ");
    }


/* EOF */
}

