/**
 * File:	include/samba-server/wizards.ycp
 * Package:	Configuration of samba-server
 * Summary:	Wizards definitions
 * Authors:	Stanislav Visnovsky <visnov@suse.cz>
 *
 * $Id$
 */

{

textdomain "samba-server";

import "Wizard";
import "Label";
import "SambaServer";

include "samba-server/dialogs.ycp";
include "samba-server/complex.ycp";

symbol MainSequence();


/**
 * Whole configuration of samba-server
 * @return sequence result
 */
symbol SambaServerSequence() ``{
    import "Sequencer";

    map aliases = $[
	"read"	: [ ``( ReadDialog() ), true ],
	"main"	:   ``( MainSequence() ),
	"write"	: [ ``( WriteDialog() ), true ]
    ];

    map sequence = $[
	"ws_start" : "read",
	"read" : $[
	    `abort	: `abort,
	    `next	: "main"
	],
	"main" : $[
	    `abort	: `abort,
	    `next	: "write"
	],
	"write" : $[
	    `abort	: `abort,
	    `next	: `next
	]
    ];

    Wizard::CreateDialog();
    Wizard::SetDesktopIcon("samba-server");

    symbol ret = Sequencer::Run(aliases, sequence);

    UI::CloseDialog();
    return ret;
}

/**
 * Whole configuration of samba-server but without reading and writing.
 * For use with autoinstallation.
 * @return sequence result
 */
symbol SambaServerAutoSequence() ``{
    // translators: inicialization dialog caption
    string caption = _("Samba Server Configuration");
    // translators: inicialization dialog contents
    term contents = `Label(_("Initializing ..."));

    Wizard::CreateDialog();
    Wizard::SetContentsButtons(caption, contents, "",
	    Label::BackButton(), Label::NextButton());

    symbol ret = MainSequence();

    UI::CloseDialog();
    
    if( ret != `abort ) {
	SambaServer::globals_configured = true;
	SambaServer::shares_configured = true;
    }
    
    return ret;
}

/**
 * Main workflow of the samba-server configuration
 * @return sequence result
 */
symbol MainSequence() ``{
    import "Sequencer";

    map aliases = $[
	"samba_role"		:   ``( RoleDialog() ),
	"enable_shares"		:   ``( OverallSharesDialog() ),
	"passdb_edit"		:   ``( PassdbDialog() ),
	"shared_directories"	:   ``( ShareDirsDialog() ),
	"share_edit"		:   ``( EditShareDialog() ),
	"ldap_edit"		:   ``( LDAPDialog() ),
    ];

    map sequence = $[
	"ws_start"	: "samba_role", 
	"enable_shares"	: $[
		`abort	: `abort,
		`next	: `next,
		`other_shares	: "shared_directories",
	],
	"samba_role"	: $[
		`abort	: `abort,
		`next	: "enable_shares",
		`advanced	: "passdb_edit",
	],
	"shared_directories"	: $[
		`abort	: `abort,
		`edit	: "share_edit",
		`next	: "enable_shares",
	],
	"passdb_edit"	: $[
		`abort	: `abort,
		`next	: "samba_role",
		`ldap	: "ldap_edit",
	],
	"share_edit"	: $[
		`abort	: `abort,
		`next	: "shared_directories",
	],
	"ldap_edit"	:  $[
		`abort	: `abort,
		`next	: "passdb_edit",
	],
    ];

    symbol ret = Sequencer::Run(aliases, sequence);

    return ret;
}

/* EOF */
}
