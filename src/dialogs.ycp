/**
 * File:	include/samba-server/dialogs.ycp
 * Package:	Configuration of samba-server
 * Summary:	Dialogs definitions
 * Authors:	Stanislav Visnovsky <stanislav.visnovsky@suse.cz>
 *
 * $Id$
 */

{

textdomain "samba-server";

import "String";
import "Label";
import "Wizard";

import "ProductFeatures";

import "Samba";
import "SambaServer";
import "SambaServerWidgets";

import "CWM";
import "CWMTab";
import "CWMServiceStart";
import "CWMFirewallInterfaces";

include "samba-server/helps.ycp";
include "samba-server/routines.ycp";
include "samba-client/routines.ycp";

string return_tab = "startup";

string pdc = _("&Primary Domain Controller (PDC)");
string bdc = _("B&ackup Domain Controller (BDC)");
string standalone = _("No Domain &Controller");

any Installation_Step1 () {

    string caption = _("Samba Installation") + " - " + _("Step 1 of 2");

    list <string> workgroups = maplist( string key,
	boolean value, Samba::PreprocessAvailableWorkgroups (), ``(key));

    // always add the currently configured workgroup
    if ( size(Samba::workgroup) > 0 && ! contains (workgroups, Samba::workgroup) )
    {
	workgroups = add ( workgroups, Samba::workgroup );
    }
    
    term contents =
	`VBox (
	    `VSquash (
		`Left( `Label ( _("Select one of the available workgroups or domains or enter your own.") ) )
	    ),
	    `VSpacing ( 1 ),
	    `VSquash (
		`Left ( `ComboBox ( `id( "workgroups" ), `opt( `editable ), _("&Workgroup or Domain Name"), workgroups ) )
	    ),
	    `VStretch ()
	);

    Wizard::SetContents ( caption, contents, HELPS["inst_step1"]:"", false, true);
	
    Wizard::HideBackButton ();
    
    if ( size(Samba::workgroup) > 0 )
    {
	UI::ChangeWidget ( `id ("workgroups"), `Value, Samba::workgroup );
    }

    any ret = nil;
    while(true) {
	ret = UI::UserInput();
	if ( ret == `next || ret == `cancel || ret == `abort) {
	    break;
	}
	else {
	    y2error( "unexpected retcode: %1", ret );
	    continue;
	}
    }
    
    if ( ret == `next )
    {
	string sel = (string)UI::QueryWidget( `id ("workgroups"), `Value );
	y2milestone ("Setting workgroup '%1'",sel);
	Samba::setWorkgroup (sel);
	SambaServer::global_config ["workgroup"] = sel;
    }
    
    Wizard::RestoreBackButton ();

    return ret;
}

any Installation_Step2 () {

    string caption = _("Samba Installation") + " - " + _("Step 2 of 2");
    
    map<string, map<string,any> > proposal = SambaServer::ProposeRole (Samba::workgroup);
    
    y2milestone ("Proposal: %1", proposal);

    term contents = `VBox(
	`VSquash(`Left(`Label( _("Current Domain Name:") + " " + Samba::workgroup))),
	`VSpacing(1),
	`VSquash(`Frame(_("Type Selection for SAMBA Server"), `VBox(
	    `RadioButtonGroup(`id("samba_server_type"), `VBox(
		`VSpacing(1),
		`Left(`RadioButton(`id(`pdc), pdc, proposal["pdc","propose"]:false)),
		`HBox(
		    `HSpacing(4),
		    `Left(`Label(proposal["pdc","message"]:""))),
		(ProductFeatures::ui_mode == "simple" ? `Empty() : `VBox(
			`Left(`RadioButton(`id(`bdc), bdc, proposal["bdc","propose"]:false)),
			`HBox(
			    `HSpacing(4),
			    `Left(`Label(proposal["bdc","message"]:""))))),
		`Left(`RadioButton(`id (`standalone), standalone, proposal["standalone","propose"]:false)),
		`VSpacing(1)))))),
	`VStretch());
	
    string help = ProductFeatures::ui_mode == "simple" ? HELPS["inst_step2_no_bdc"]:"" :  HELPS["inst_step2"]:"";

    Wizard::SetContents ( caption, contents, help, true, true );
	
    UI::ChangeWidget ( `id (`pdc), `Enabled, proposal["pdc","possible"]:true );
    if (ProductFeatures::ui_mode != "simple") {
	UI::ChangeWidget ( `id (`bdc), `Enabled, proposal["bdc","possible"]:true );
    }

    any ret = nil;
    while(true) {
	ret = UI::UserInput();
	if ( ( ret == `abort || ret == `cancel ) && ! SambaServer::AbortFunction () )
	{
	    continue;
	}
	if ( ret == `back || ret == `next || ret == `abort || ret == `cancel ) {
	    if ( ret == `next )
	    {
		// store the value
		SambaServer::setRole ( (symbol) UI::QueryWidget (
		    `id ( "samba_server_type" ),
		    `CurrentButton
		));
	    }
	    break;
	}
	else {
	    y2error( "unexpected retcode: %1", ret );
	    continue;
	}
    }

    return ret;
}

/************************* initial wizard end **************************/

void BaseSettingsWidgetInit(string key)
{
    UI::ChangeWidget(`id("workgroup_domainname"), `Value, Samba::workgroup);
    UI::ChangeWidget(`id("domain_controller"), `Value, SambaServer::role);
}

void WinsSettingsWidgetInit(string key)
{
    boolean wins = SambaServer::global_config["wins support"]:false;

    UI::ChangeWidget(`id("wins_server_support"), `Value, wins);
    UI::ChangeWidget(`id("remote_wins_server"), `Value, ! wins);
    UI::ChangeWidget(`id("wins_server_name"), `Enabled, ! wins);
    UI::ChangeWidget(`id("wins_server_name"), `Value, SambaServer::global_config["wins server"]:"" );
}

void GlobalConfigStringWidgetInit(string key)
{
    UI::ChangeWidget(`id(key), `Value, SambaServer::global_config[key]:"" );
}

void BaseSettingsWidgetStore(string key, map event_descr)
{
    string sel = (string)UI::QueryWidget(`id("workgroup_domainname"), `Value);
    y2milestone("Setting workgroup '%1'",sel);
    Samba::setWorkgroup(sel);
    SambaServer::global_config["workgroup"] = sel;

    symbol role = (symbol)UI::QueryWidget(`id("domain_controller"), `Value);
    y2milestone ("Setting role '%1'",role);
    SambaServer::setRole(role);
    
    SambaServer::modified = true;
}

void WinsSettingsWidgetStore(string key, map event_descr)
{
    if ( (boolean)UI::QueryWidget(`id("wins_server_support"), `Value)) {
	y2milestone("Enabling wins support");
	SambaServer::global_config["wins support"] = true;
    } else {
	SambaServer::global_config ["wins support"] = false;
	SambaServer::global_config ["wins server"] = (string)UI::QueryWidget(`id("wins_server_name"), `Value);
	y2milestone("Disabling wins support, using server '%1'", SambaServer::global_config["wins server"]:nil );
    }

    SambaServer::modified = true;
}

void GlobalConfigStringWidgetStore(string key, map event_descr)
{
    string value = (string)UI::QueryWidget(`id(key), `Value);
    
    // warn about the netbios name change
    if (key == "netbios name" && !Mode::config && value != SambaServer::global_config[key]:"") {
	Popup::Warning( warnings["netbios"]:"" );
    }
    
    if (value != SambaServer::global_config[key]:"") {
	SambaServer::global_config[key] = value;
        SambaServer::modified = true;
    }
}

symbol WinsSettingsWidgetHandle(string key, map event_descr)
{
    any event = event_descr["ID"]:nil;
    if ( event == "wins_server_support" || event == "remote_wins_server" ) {
	UI::ChangeWidget(`id("wins_server_name"), `Enabled, event == "remote_wins_server");
    }
    return nil;
}

symbol AdvancedSettingsWidgetHandle(string key, map event)
{
    if (is(event["ID"]:nil, symbol) && contains([ `ldap, `passdb, `global_settings], event["ID"]:`cancel)) {
	return_tab = "identity";
	return event["ID"]:`next;
    }
    return nil;
}

void SharesWidgetInit(string key)
{
    list items = shares2items( SambaServer::shares );
    
    UI::ChangeWidget( `id(`table ), `Items, items );
    UI::ChangeWidget( `id(`edit), `Enabled, size(SambaServer::shares)>0 );
    UI::ChangeWidget( `id(`delete), `Enabled, size(SambaServer::shares)>0 );
    UI::ChangeWidget( `id(`toggle), `Enabled, size(SambaServer::shares)>0 );
}

symbol SharesWidgetHandle(string key, map event_descr)
{
    if (! is(event_descr["ID"]:nil, symbol)) return nil;
    symbol ret = (symbol) event_descr["ID"]:nil;
    return_tab = "shares";

    if (ret == `add || ret == `ldap || ret == `passdb ) {
	return ret;
    }
    if (ret == `filter_all) {
	list items = shares2items( SambaServer::shares );
	UI::ChangeWidget ( `id( `table ), `Items, items );
	return nil;
    }
    if (ret == `filter_non_system) {
	list<string> system = ["homes","printers","print$"];
	list items = shares2items(filter(string s, map value, SambaServer::shares, ``(!contains(system, s))));
	UI::ChangeWidget(`id(`table ), `Items, items);
	return nil;
    }

    integer id = (integer) UI::QueryWidget(`id(`table), `CurrentItem);
    if (id == nil) return nil;
	
    string share = (string) select( (term) UI::QueryWidget(`id(`table), `Item(id) ), 2, nil );
    if (share == nil) return nil;
    
    if (ret == `edit) {
	SambaServerWidgets::shareToEdit = share;
	return `edit;
    }
    if (ret == `toggle) {
	SambaServer::enableShare( share, (SambaServer::shares[share, "commentout"]:false) );
	UI::ChangeWidget( `id(`table), `Items, shares2items( SambaServer::shares ) );
	UI::ChangeWidget( `id(`table), `CurrentItem, id );
	SambaServer::modified = true;
	return nil;
    }
    if (ret == `delete)
    {
	// confirmation dialog before deleting a share
	if  (Popup::ContinueCancel( sformat( _("Really 
delete share '%1'?

All its settings will be lost.
"), share ) ) ) {
	    SambaServer::removeShare( share );
	    UI::ChangeWidget( `id(`table), `Items, shares2items( SambaServer::shares ) );
	}
    }
    
    return nil;
}

void AddTrustedDomain ()
{
    term contents = `VBox (
	`Label (_("Enter the name of the trusted domain
and the password to access it.")),
	`VSpacing (1),
	`TextEntry (`id(`domain), _("&Domain:") ),
	`Password (`id(`password), _("&Password:") ),
	`VSpacing (1),
	`HBox (
	    `PushButton (`id (`ok), `opt (`default), Label::OKButton () ),
	    `PushButton (`id (`cancel), Label::CancelButton () )
	)
    );
    
    UI::OpenDialog (contents);
    UI::SetFocus (`id (`domain) );

    symbol ret = nil;    
    do 
    {
	ret = (symbol)UI::UserInput ();
	
	if (ret == `ok)
	{
	    string domain = (string) UI::QueryWidget (`domain, `Value);
	    string passwd = (string) UI::QueryWidget (`password, `Value);
	    
	    if ( size(domain) == 0 )
	    {
		import "Report";
		Report::Error (_("Domain name cannot be empty.") );
		ret = nil;
	    }
	    if (! SambaServer::EstablishTrustedDomain (domain, passwd) )
	    {
		import "Report";
		Report::Error (_("Cannot establish trusted domain relationship."));
		ret = nil;
	    }
	    
	    SambaServer::modified = true;
	}
    } while (ret == nil );
    
    
    UI::CloseDialog ();
}

boolean autoyast_warning_done = false;

void TrustedDomainsWidgetInit(string key)
{

    if ( Mode::config && !autoyast_warning_done)
    {
	// issue a warning, if not already done so
	Popup::Warning (_("YaST needs to store a password for trusted domains
in the autoinstallation control file. The password
is stored as plain text. This can be considered
a security threat."));
	autoyast_warning_done = true;
    }
    
    list<term> domains = [];
    
    foreach (string key, boolean value, SambaServer::trusted_domains, ``{
	if (value) 
	{
	    domains = add (domains, `item ( `id(key), key ) );
	}
    });
    
    UI::ReplaceWidget(`id(`domains_tr), `SelectionBox(`id("trusted_domains"), _("&Trusted Domains List"), domains));

    // disable delete button if needed
    UI::ChangeWidget (`id (`delete_domain), `Enabled, size (domains) != 0);
}

symbol TrustedDomainsWidgetHandle (string key, map event_descr)
{
    if ( event_descr["ID"]:nil == `add_domain )
    {
	AddTrustedDomain ();
    }
    else if ( event_descr["ID"]:nil == `delete_domain )
    {
	string to_delete = (string)UI::QueryWidget(`id("trusted_domains"), `CurrentItem );
	    
	// confirmation
	if  (Popup::ContinueCancel( sformat( _("Really abadon trust relationship 
to trusted domain '%1'?"), to_delete ) )  )
	{
	    SambaServer::trusted_domains[to_delete] = false;
	    
	    SambaServer::modified = true;
	}
    }

    // reinitialize contents
    TrustedDomainsWidgetInit (key);

    return nil;
}

/**
 * EditShareDialog dialog
 * @return dialog result
 */
symbol EditShareDialog () ``{

    term contents = `HBox (`HSpacing (1), `VBox (
        `VSpacing (1), 
        "share_edit"
    ), `HSpacing (1));

    // dialog caption
    string caption = sformat( _("Edit Share \"%1\""), SambaServerWidgets::shareToEdit );


    return CWM::ShowAndRun($[
	"widget_names" : ["share_edit"],
	"widget_descr" : SambaServerWidgets::widgets,
	"contents" : contents,
	"caption" : caption,
	"back_button" : Label::BackButton(),
	"next_button" : Label::OKButton(),
	"fallback_functions" : $[`abort: SambaServer::AbortFunction]
    ]);
}

symbol GlobalSettingsDialog () ``{

    term contents = `HBox (`HSpacing (1), `VBox (
        `VSpacing (1), 
        "globalsettings"
    ), `HSpacing (1));

    // dialog caption
    string caption = _("Expert Global Settings Configuration");

    symbol res = CWM::ShowAndRun($[
	"widget_names" : ["globalsettings"],
	"widget_descr" : SambaServerWidgets::widgets,
	"contents" : contents,
	"caption" : caption,
	"back_button" : Label::BackButton(),
	"next_button" : Label::OKButton(),
	"fallback_functions" : $[`abort: SambaServer::AbortFunction]
    ]);

    if (res == `next) {
	// update the rest of the settings using the entered ones
	Samba::setWorkgroup ( SambaServer::global_config["workgroup"]: (Samba::workgroup) );	
	SambaServerPassdb::SetBackends ( splitstring( SambaServer::global_config["passdb backend"]:"", " ") );
	SambaServer::role = SambaServer::DetermineRole();
    }
    
    return res;
}

symbol AddShareDialog () ``{

    term contents = `HVSquash (`HBox (`HSpacing (1), `VBox ( `opt (`hstretch),
	`VSpacing (1),
	`Frame ( _("Identification:"),
	    `VBox (
		`TextEntry ( `id (`name), _("Share &Name:") ),
		`TextEntry (`id (`comment), _("Share &Description:") )
	    )
	),
	`Frame ( _("Share Type:"),
	    `HBox (
		`HSpacing ( 1 ),
		`VBox ( `opt ( `hstretch ),
		    `RadioButtonGroup( 
		    `VBox (
			`Left( `RadioButton( `id ( `printer), `opt( `notify ), _("&Printer") ) ),
			`Left( `RadioButton( `id ( `directory), `opt( `notify ), _("&Directory"), true ) )
		    )
		),
                `HBox(
            	    // translators: text entry label
            	    `TextEntry( `id(`path), _("Share &Path:"), "/home" ),
            	    `Bottom( `PushButton( `id( `browse ), Label::BrowseButton() ) )
        	)),
		`HSpacing ( 1 )
	    )
	)
    )));
    
    // translators: dialog caption
    string caption = _("Add New Share");

    Wizard::SetContentsButtons ( caption, contents, HELPS["add_share"]:"",
	Label::BackButton (), Label::OKButton () );
	
    UI::SetFocus ( `id (`name));

    symbol ret = nil;
    
    do 
    {
	// enable/disable path
	boolean on = (boolean)UI::QueryWidget ( `id( `directory ), `Value );
	UI::ChangeWidget ( `id (`path), `Enabled, on );
	UI::ChangeWidget ( `id (`browse), `Enabled, on );
	
	ret = (symbol)UI::UserInput ();
	
	if (ret == `printer || ret == `directory)
	{
	    ret = nil;
	    continue;
	}
	
	if (ret == `browse)
	{
            // translators: file selection dialog title
            string dir = UI::AskForExistingDirectory ( 
		(string)UI::QueryWidget (`id(`path), `Value)
		, _("Path for a Share") );
            if( dir != nil ) {
                UI::ChangeWidget( `id (`path), `Value, dir );
            }
            ret = nil;

	} else if (ret == `next) 
	{
	    // OK was pressed
	
	    string name = (string) UI::QueryWidget( `id( `name ), `Value );
	    string pathvalue = (string) UI::QueryWidget( `id( `path ), `Value );
	    string comment = (string) UI::QueryWidget( `id( `comment ), `Value );
	    boolean printable = (boolean) UI::QueryWidget (`id ( `printer ), `Value );
	
	    if( size( name ) == 0 ) {
		// translators: error message
		Popup::Error( _("Share name cannot be empty.") );
		ret = nil;
		continue;
	    } else if( size( pathvalue ) == 0 && ! printable ) {
		// translators: error message
		Popup::Error( _("Share path cannot be empty.") );
		ret = nil;
		continue;
	    } else if ( ! SambaServerWidgets::SharePathWarning (pathvalue) ) {
		ret = nil;
		continue;
	    } else if( !printable && ! Mode::config && SCR::Read( .target.stat, pathvalue ) == $[] ) {
		// translators: question popup. A user entered non-existent path
		// for a share
		if( !Popup::ContinueCancel( sformat( _("The path '%1' does not exist.
Really use this path?
"), pathvalue ) ) ) {
		    ret = nil;
		    continue;
		}
	    }
	    
	    map<string,any> res = $[
            	    "comment": comment,
            	];
	    
	    if (printable) 
	    {
		res["printable"] = true;
		res["path"] = "/var/tmp";
	    }
	    else
	    {
		res["writeable"] = true;
		res["path"] = pathvalue;
	    }

	    if (SambaServer::addShare( name , res ) != true) {
		// translators: popup error message for "add share"
		Popup::Error( sformat( _("Share '%1' already exists."), name ) );
		ret = nil;
	    }
	}
    } while( ret == nil );
    return ret;
}

boolean TabAbort () {
    SambaServer::modified = true;
    return SambaServer::AbortFunction ();
}

any Installation_Conf_Tab () {

    term shares_widget = `HBox(
	`HWeight(1, `Empty()),
	`HWeight(100, `VBox(
	    `HBox(
		`Left(`Label(_("Available shares are"))),
	        `HStretch(),
		`Right(`MenuButton(_("&Filter"), [
		    `item(`id(`filter_all), "Show &All Shares" ),
		    `item(`id(`filter_non_system), "Do Not Show &System Shares" )]))),
	    // translators: table header texts
	    `Table( `id(`table ), `opt(`hvstretch), `header(_("Status"), _("Name"),  _("Path"), _("Comment")), []),
	    `HBox(
		`PushButton(`id(`add), Label::AddButton() + "..."),
		`PushButton(`id(`edit), Label::EditButton() + "..."),
		`PushButton(`id(`delete), Label::DeleteButton()),
		`HStretch(),
		`PushButton(`id(`toggle), _("&Toggle Status"))))),
	`HWeight(1, `Empty()));

    term wins_widget = `Frame(_("WINS"), `HBox(
	`HSpacing(1),
	`VBox(
	    `VSpacing(0.5),
	    `RadioButtonGroup(`id("wins_support"),`VBox(
		`Left(`RadioButton(`id("wins_server_support"), `opt(`notify ), _("WINS Server Support"))),
		`Left(`RadioButton(`id("remote_wins_server"), `opt(`notify ), _("Remote WINS Server"))))),
	    `HBox(
	        `HSpacing(3),
		`TextEntry(`id("wins_server_name"), _("Na&me:"))),
	    `VStretch()),
	`HSpacing(1)));

    list<term> roles = [
	// translators: combobox item
	`item(`id(`standalone), _("No DC")),
	// translators: combobox item
	`item(`id(`pdc), _("Primary (PDC)"))
    ];

    if (ProductFeatures::ui_mode != "simple") {
	// translators: combobox item
	roles = add( roles, `item ( `id ( `bdc ), _("Backup (BDC)") ));
    }
    
    term basesettings_widget = `Frame(_("Base Settings"), `HBox(
        `HSpacing(1),
	`VBox(
	    `ComboBox(`id("workgroup_domainname"), `opt(`editable, `hstretch), _("&Workgroup or Domain Name"),
		maplist( string key, boolean value, Samba::PreprocessAvailableWorkgroups (), ``(key))),
	    // translators: combobox label
	    `ComboBox(`id("domain_controller"), `opt(`hstretch), _("Domain &Controller"),  roles),
	    `VStretch()),
	`HSpacing(1)));
    
    term advanced_settings_widget = `MenuButton(_("Advanced Settings..."), [
	`item(`id(`global_settings), _("&Expert Global Settings")),
	`item(`id(`ldap), _("&LDAP Settings")),
	`item(`id(`passdb), _("&User Authentication Sources")) ] );
	
    term trusted_domains_widget = `HBox(
	`HWeight(1, `Empty()),
	`HWeight(100, `VBox(
	    `VWeight(7, `ReplacePoint(`id(`domains_tr),
		`SelectionBox(`id("trusted_domains"), _("&Trusted Domains List"), [] ))),
	    `VWeight(1, `HBox(
		`PushButton(`id(`add_domain), Label::AddButton() + "..."),
		`PushButton(`id(`delete_domain), Label::DeleteButton()),
		`HStretch())),
	    `VStretch())),
	`HWeight(1, `Empty()));

    string caption = _("Samba Configuration");

    map tabs_descr = $[
	"startup": $[
            "header" : _("Start &Up"),
	    "contents": `HBox(
		`HWeight(2, `Empty()),
		`HWeight(100, `VBox(
		    `VSpacing(1),
		    "SERVICE START",
		    `VSpacing(1),
		    `Frame(_("Firewall Settings"), "FIREWALL"),
		    `VStretch())),
		`HWeight(2, `Empty())),
	    "widget_names": ["SERVICE START", "FIREWALL"]
	],
	"shares": $[
            "header" : _("&Shares"),
	    "contents": `VBox("SHARES"),
	    "widget_names": ["SHARES"]
	],
	"identity": $[
            "header" : _("I&dentity"),
	    "contents": `HBox( "IDENTITY COMMON HELP",
		`HWeight(1, `Empty()),
		`HWeight(100, `VBox(
		    `VSpacing(0.5),
		    (ProductFeatures::ui_mode == "simple" ? 
			`HBox(
			    `HWeight(25, `Empty()),
			    `HWeight(50, `VBox(
				"BASE SETTINGS",
				`VSpacing(0.5),
				"netbios name",
				"ADVANCED SETTINGS")),
			    `HWeight(25, `Empty())) :
			`VBox(
			    `HBox(
				`HWeight(1, "BASE SETTINGS"),
				`HSpacing(0.5),
				`HWeight(1, "WINS SETTINGS")),
			    `VSpacing(0.5),
			    "netbios name",
			    "ADVANCED SETTINGS")),
		    `VStretch())),
		`HWeight(1, `Empty())),
	    "widget_names": ["IDENTITY COMMON HELP", "BASE SETTINGS", "WINS SETTINGS", "netbios name", "ADVANCED SETTINGS"]
	]
    ];
    
    if (ProductFeatures::ui_mode != "simple") {
	tabs_descr = union(tabs_descr, $[
	    "trusted_domains_tab": $[
		"header": _("&Trusted Domains"),
		"contents": `VBox("TRUSTED DOMAINS"),
		"widget_names": ["TRUSTED DOMAINS"]
	    ]
	]);
    }
    
    map tabs_widget_descr = $[
	"SERVICE START": CWMServiceStart::CreateAutoStartWidget($[
	    "get_service_auto_start": SambaServer::ServiceEnabled,
	    "set_service_auto_start": SambaServer::setServiceEnabled,
	]),
	"FIREWALL": CWMFirewallInterfaces::CreateOpenFirewallWidget($[
	    "services": [ "samba-server" ],
	    "display_details": true
	]),
	"SHARES": $[
	    "widget" : `custom,
	    "custom_widget" : shares_widget,
	    "init" : SharesWidgetInit,
	    "handle" : SharesWidgetHandle,
	    "help": HELPS["smb_conf_tab_shares"]:""
	],
	"IDENTITY COMMON HELP": $[
	    "widget" : `custom,
	    "custom_widget" : `Empty(),
	    "help": HELPS["smb_conf_tab_identity"]:""
	],
	"BASE SETTINGS": $[
	    "widget" : `custom,
	    "custom_widget" : basesettings_widget,
	    "help": ProductFeatures::ui_mode == "simple" ? HELPS["smb_conf_tab_base_settings_no_bdc"]:"" : HELPS["smb_conf_tab_base_settings"]:"",
	    "init": BaseSettingsWidgetInit,
	    "store": BaseSettingsWidgetStore,
	],
	"WINS SETTINGS": $[
	    "widget" : `custom,
	    "custom_widget" : wins_widget,
	    "help": HELPS["smb_conf_tab_wins_settings"]:"",
	    "init": WinsSettingsWidgetInit,
	    "store": WinsSettingsWidgetStore,
	    "handle": WinsSettingsWidgetHandle,
	],
	"netbios name": $[
	    "widget": `textentry,
	    "label": _("NetBIOS &Host Name"),
	    "help": HELPS["smb_conf_tab_netbios_name"]:"",
	    "init": GlobalConfigStringWidgetInit,
	    "store": GlobalConfigStringWidgetStore,
	],
	"ADVANCED SETTINGS": $[
	    "widget" : `custom,
	    "custom_widget" : advanced_settings_widget,
	    "help": HELPS["smb_conf_tab_advanced_settings"]:"",
	    "handle": AdvancedSettingsWidgetHandle,
	],
	"TRUSTED DOMAINS": $[
	    "widget": `custom,
	    "custom_widget": trusted_domains_widget,
	    "init": TrustedDomainsWidgetInit,
	    "handle": TrustedDomainsWidgetHandle,
	    "help": HELPS["smb_conf_tab_trusted_domains"]:"",
	]
    ];

    map widget_descr = $[
	"tab": CWMTab::CreateWidget($[
	    "tab_order": ProductFeatures::ui_mode != "simple" ?
		["startup", "shares", "identity", "trusted_domains_tab"] :
		["startup", "shares", "identity"],
	    "tabs": tabs_descr,
	    "widget_descr": tabs_widget_descr,
	    "initial_tab" : return_tab,
	    "tab_help" : "",
	]),
    ];

    term contents = `VBox( "tab", `VStretch ());

    list<map <string, any> > w = CWM::CreateWidgets (["tab"], (map <string, map <string, any> >)widget_descr);
    string help = CWM::MergeHelps(w);
    contents = CWM::PrepareDialog(contents, w);

    Wizard::SetContentsButtons(caption, contents, help, Label::NextButton (), Label::FinishButton ());
    Wizard::HideBackButton();
    
    any ret = CWM::Run(w, $[`abort: TabAbort ]);
    
    Wizard::RestoreBackButton ();
    
    return ret;
}


/**
 * Dialog to configure passdb backends.
 * @return dialog result
 */
symbol PassdbDialog () {
    // dialog caption
    string caption = _("User Information Sources");

    list<map<string, any> > w = CWM::CreateWidgets (["passdb_edit"], (map <string, map <string, any> >)SambaServerWidgets::widgets);

    term contents = `HBox (`HSpacing (1), `VBox (
        `VSpacing (1), 
	w[0, "widget"]:`VSpacing (0)
    ), `HSpacing (1));

    string help = CWM::MergeHelps (w);

    Wizard::SetContentsButtons (caption, contents, help,
        Label::BackButton (), Label::OKButton ());

    UI::ReplaceWidget( `id(`_tp_table_repl),
        // translators: menu button label for accessing the LDAP-related settings and actions
        `MenuButton( "LDAP",
	    // translators: menu item to show a LDAP-related settings
	    [ `item( `id(`ldap), _("Global LDAP Settings") ),
	      // translators: menu item to test the currently selected LDAP url
	      `item( `id(`ldap_test), _("Test LDAP Connection") ),
	    ]
	) 
    );

    return CWM::Run (w, $[`abort: SambaServer::AbortFunction]);
}

/**
 * LDAP Dialog
 * @return dialog result
 */
symbol LDAPDialog () ``{

    /* Samba-server advanced dialog caption */
    string caption = _("LDAP Samba Server Options");

    /* Samba-server advanced dialog contents */
    term contents = 
    `VBox(
	// text entry label
	`TextEntry( `id( `ldap_suffix ), _("Search &Base DN:"), 
	    SambaServer::LDAP_suffix != nil ? SambaServer::LDAP_suffix: "" ),
	// text entry label
	`TextEntry( `id( `ldap_admin_dn ), _("&Administration DN:"), 
	    SambaServer::LDAP_admin_dn != nil ? SambaServer::LDAP_admin_dn : ""  ),
	Mode::config ? `Empty() : 
	    `HBox(
		// push button label
		`PushButton( `id( `ldap_password ), _("Set LDAP Administration &Password") )
	    )
    );

    Wizard::SetContentsButtons(caption, contents, HELPS["Advanced"]:"",
	    Label::BackButton(), Label::OKButton());

    any ret = nil;
    while(true) {

	ret = UI::UserInput();
	
	/* abort? */
	if(ret == `abort || ret == `cancel) {
	    if(SambaServer::AbortFunction()) break;
	    else continue;
	}
	else if(ret == `ldap_password ) {
	    // Translators: popup query for a password
	    string passwd = passwordPopup( sformat( _("Enter LDAP Administration Password"), Samba::workgroup ) );
	    // user cancelled
	    if( passwd == nil ) continue;
	    
	    string admin_dn = (string) UI::QueryWidget( `id( `ldap_admin_dn ), `Value );
	    string suffix = (string) UI::QueryWidget( `id( `ldap_suffix ), `Value );
	    if (size(admin_dn) == 0 || size(suffix) == 0) 
	    {
		y2milestone ("Empty admin dn or suffix");
		// FIXME: better message after freeze
		Popup::Error( _("Cannot set the password.") );
		continue;
	    }
	    
	    // store the current bind DN
	    SambaServer::setLDAPAdminDN( admin_dn  );
	    
	    string error = SambaServer::setLDAPAdminPassword( passwd );
	    if(  error != nil )
	    {
		Popup::Error( error );
	    }
	    // Translators: Information popup
	    else Popup::Message( _("LDAP administration password was successfully changed.") );
	}
        else if(ret == `next ) {
	    SambaServer::setLDAPAdminDN( (string) UI::QueryWidget( `id( `ldap_admin_dn ), `Value ) );
	    SambaServer::setLDAPSuffix( (string) UI::QueryWidget( `id( `ldap_suffix ), `Value ) );

            break;
        }
	else if(ret == `back ) {
	    break;
	}
        else {
            y2error("unexpected retcode: %1", ret);
            continue;
        }
    }
    
    return (symbol) ret;
}

/* EOF */
}
