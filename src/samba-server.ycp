/**
 * File:	clients/samba-server.ycp
 * Package:	Configuration of samba-server
 * Summary:	Main file
 * Authors:	Stanislav Visnovsky <visnov@suse.cz>
 *
 * $Id$
 *
 * Main file for samba-server configuration. Uses all other files.
 */

{

/***
 * <h3>Configuration of the samba-server</h3>
 */

textdomain "samba-server";

import "CommandLine";
import "Report";
import "Require";
import "Samba";
import "SambaServer";

include "commandline/commandline.ycp";

/**
 * Command line "share" commands handler.
 *
 * @param options	map of options from command line
 * @return boolean	true on success
 */
global define boolean ShareHandler(map options) ``{
    string share = options["name"]:nil;
    
    // check the "command" to be present exactly once
    string command = CommandLine::UniqueOption( options, 
	["add", "delete", "enable", "disable", "options", "show" ] );
    if( command == nil ) return false;

    // validate the options
    if( share == nil ) {
	// must provide the share name
	Report::Error( sformat( _("You must specify the name of a share.") ) );
	return false;
    }
    
    if( !haskey( SambaServer::shares, share ) && command != "add" ) {
	Report::Error( sformat( _("The share '%1' does not exist."), share ) );
	return false;
    }
    
    // process the commands
    if( command == "enable" )
    {
	SambaServer::enableShare( share, true );
    }
    else if( command == "disable" )
    {
	SambaServer::enableShare( share, false );
    }
    else if( command == "delete" )
    {
	SambaServer::removeShare( share );
    }
    else if( command == "add" )
    {
	if( ! haskey( options, "path" ) ) {
	    Report::Error( _("You must provide the path of a directory to be shared.") );
	    return false;
	}
	
	if( !SambaServer::addShare( share, $[
	    "path": options["path"]:"",
	    "comment": options["comment"]:sformat( "Share for %1", options["path"]:"path" )
	] ) ) {
	    Report::Error( sformat( _("Share '%1' already exists."), share ) );
	    return false;
	}
    }
    else if( command == "show" )
    {
	CommandLine::Print( sformat("[%1]", share) );
	
	foreach( string option, any value, SambaServer::shares[share]:$[], ``{
	    if( value != nil )
		CommandLine::Print( sformat( "\t%1 = %2", option, value ) );
	});
    }
    else if( command == "options" )
    {
	map opts = eval(SambaServer::shares[share]:$[]);
	
	string value = options["comment"]:nil;
	if( value != nil ) opts["comment"] = value;
	
	value = options["path"]:nil;
	if( value != nil ) opts["path"] = value;

	value = options["printable"]:nil;
	if( value != nil ) opts["printable"] = normalize_boolean(value, false);

	value = options["write list"]:nil;
	if( value != nil ) opts["write list"] = value;

	value = options["browseable"]:nil;
	if( value != nil ) opts["browseable"] = normalize_boolean(value, true);

	value = options["guest_ok"]:nil;
	if( value != nil ) opts["guest ok"] = normalize_boolean(value, false);

	value = options["valid_users"]:nil;
	if( value != nil ) opts["valid users"] = value;
	
	SambaServer::updateShare( share, opts );
    }
    
    return true;
}

/**
 * Command line "list" command handler.
 *
 * @param options	map of options from command line
 * @return boolean	true on success
 */
global define boolean ListHandler( map options ) ``{
    
    // heading
    CommandLine::Print( _("Status  \tType\tName"));
    CommandLine::Print( _("=============================="));

    map printers = $[];
    
    foreach( string share, map options, SambaServer::shares, ``{
	if( options["printable"]:false ) {
	    printers[share] = options;
	} else {
	    CommandLine::Print( sformat( _("%1\tDisk\t%2"), 
		options["commentout"]:false ? _("Disabled"): _("Enabled")+"  ", share ) );
	}
    });
    

    foreach( string share, map options, printers, ``{
	CommandLine::Print( sformat( _("%1\tPrinter\t%2"), 
	    options["commentout"]:false ? _("Disabled"): _("Enabled")+"  ", share ) );
    });
    
    return true;
}

/**
 * Command line "backend" command handler.
 *
 * @param options	map of options from command line
 * @return boolean	true on success
 */
global define boolean BackendHandler( map options ) ``{
    // check the backend to be present exactly once
    string command = CommandLine::UniqueOption( options, 
	["smbpasswd", "ldap" ] );
    if( command == nil ) return false;

    symbol backend = nil;
    if( command == "smbpasswd" ) backend = `smbpasswd;
    else if( command == "ldap" ) backend = `ldap;
     
    if( ! SambaServer::switchLDAPBackend( backend ) ) {
	Report::Error( sformat( _("Cannot change the backend of the Samba server to '%1'."), command ) );
	return false;
    }
    
    return true;
}

/**
 * Command line "role" command handler.
 *
 * @param options	map of options from command line
 * @return boolean	true on success
 */
global define boolean RoleHandler( map options ) ``{
    // check the role to be present exactly once
    string command = CommandLine::UniqueOption( options, 
	["pdc", "bdc", "standalone" ] );
    if( command == nil ) return false;
    
    symbol role = nil;
    
    if( command == "pdc" ) role = `pdc;
    else if( command == "bdc" ) role = `bdc;
    else if( command == "standalone" ) role = `standalone;
    
    if( ! SambaServer::setRole( role ) ) {
	Report::Error( sformat( _("Cannot change the role of the Samba server to '%1'."), command ) );
	return false;
    }
    
    return true;
}

/**
 * Command line "service" command handler.
 *
 * @param options	map of options from command line
 * @return boolean	true on success
 */
global define boolean SambaServerEnableHandler( map options ) ``{
    // check the "command" to be present exactly once
    string command = CommandLine::UniqueOption( options, 
	["enable", "disable" ] );
    if( command == nil ) return false;

    if( command == "enable" ) {
	// TODO: maybe we should switch back to the original role (use the heuristics again)
	if( SambaServer::role == `disabled ) SambaServer::setRole( `standalone );
	return SambaServer::enableServer( true );
    }

    if( command == "disable" ) {
	SambaServer::setRole( `disabled );
	return SambaServer::enableServer( false );
    }
    
    // should not happen
    return false;
}

/**
 * Command line "configure" command handler.
 *
 * @param options	map of options from command line
 * @return boolean	true on success
 */
global define boolean ChangeConfiguration( map options ) ``{
	
    string value = options["workgroup"]:nil;
    if( value != nil ) Samba::setWorkgroup(value);

    string value = options["description"]:nil;
    if( value != nil ) SambaServer::setDescription(value);

    string value = options["ldap_server"]:nil;
    if( value != nil ) SambaServer::setLDAPServer(value);

    string value = options["ldap_suffix"]:nil;
    if( value != nil ) SambaServer::setLDAPSuffix(value);

    string value = options["ldap_admin_dn"]:nil;
    if( value != nil ) SambaServer::setLDAPAdminDN(value);

    return true;
}

/* The main () */
y2milestone ("----------------------------------------");
y2milestone ("Samba-server module started");

include "samba-server/wizards.ycp";

/* main ui function */
any ret = nil;

map cmdline = $[
    "id" 		: "samba-server",
    "help" 		: _("Samba server configuration module.
See Samba documentation for details."),
    "guihandler" 	: ``(SambaServerSequence()),
    "initialize"	: ``(SambaServer::Read()),
    "finish"		: ``(SambaServer::Write()),
    "actions"		: $[
	"share"	:$[
	    "handler"	: ``(ShareHandler()),
	    "help"	: _("Manipulate a single share")
	],
	"list"	:$[
	    "handler"	: ``(ListHandler()),
	    "help"	: _("Show the list of available shares")
	],
	"role"	:$[
	    "handler"	: ``(RoleHandler()),
	    "help"	: _("Set a role of the server")
	],
	"backend" :$[
	    "handler"	: ``(BackendHandler()),
	    "help"	: _("Set the backend for storing user information")
	],
	"service" :$[
	    "handler"	: ``(SambaServerEnableHandler()),
	    "help"	: _("Enable or disable the Samba services (smb and nmb)")
	],
	"configure"	: $[
	    "handler"	: ``(ChangeConfiguration()),
	    "help"	: _("Change the global settings of the Samba server")
	]
    ],
    "options"		: $[
	"enable" 	:$[
	    "help"	: _("Enable the share or a service")
	],
	"disable"	:$[
	    "help"	: _("Disable the share or a service")
	],
	"delete"	:$[
	    "help"	: _("Remove the share from the configuration file" )
	],
	"name"		:$[
	    "help"	: _("The name of a share"),
	    "type"	: "string"
	],
	"add"		:$[
	    "help"	: _("Add a new share"),
	], 
	"options"	:$[
	    "help"	: _("Change options of a share"),
	], 
	"show"		:$[
	    "help"	: _("Show the options of a shae"),
	], 
	"comment"	:$[
	    "help"	: _("The comment of a share"),
	    "type"	: "string"
	], 
	"path"		:$[
	    "help"	: _("The path (directory) to be shared"),
	    "type"	: "string"
	], 
	"printable"	:$[
	    "help"	: _("Flag if the share should act as a printer"),
	    "type"	: "boolean"
	], 
	"read_list"	:$[
	    "help"	: _("A comma separated list of users allowed to read from the share"),
	    "type"	: "string"
	], 
	"write_list"	:$[
	    "help"	: _("A comma separated list of users allowed to write to the share"),
	    "type"	: "string"
	], 
	"browseable"	:$[
	    "help"	: _("Flag if the share should be visible by browsing LAN"),
	    "type"	: "boolean"
	], 
	"guest_ok"	:$[
	    "help"	: _("Flag if the share should allow guest access"),
	    "type"	: "boolean"
	], 
	"valid_users"	:$[
	    "help"	: _("A comma separated list of users allowed to access the share"),
	    "type"	: "string"
	],
	"pdc"		:$[
	    "help"	: _("Server should act as a Primary Domain Controller"),
	], 
	"bdc"		:$[
	    "help"	: _("Server should act as a Backup Domain Controller"),
	], 
	"standalone"	:$[
	    "help"	: _("Server should provide shares, but should not allow domain logons."),
	],
	"smbpasswd"	:$[
	    "help"	: _("Use the 'smbpasswd' file to store user information"),
	],
	"ldap"		:$[
	    "help"	: _("Use the LDAP server to store user information"),
	],
	"workgroup"	:$[
	    "help"	: _("The name of a workgroup"),
	    "type"	: "string"
	], 
	"description"	:$[
	    "help"	: _("The human-readable description for the Samba server"),
	    "type"	: "string"
	], 
	"ldap_server"	:$[
	    "help"	: _("The LDAP server to be used"),
	    "type"	: "host"
	], 
	"ldap_suffix"	:$[
	    "help"	: _("The LDAP suffix DN used for manipulating the user information on LDAP server"),
	    "type"	: "string"
	], 
	"ldap_admin_dn"	:$[
	    "help"	: _("The LDAP DN for modifying contents of the LDAP server (for example changing passwords)"),
	    "type"	: "string"
	]
    ],
    "mappings"		: $[
	"share"		: [ "enable", "disable", "delete", "add", "options", "show", 
	    "name", "comment", "path", 
	    "printable", "read_list", "write_list", "browseable", "guest_ok", "valid_users" ],
	"list"		: [ ],
	"role"		: [ "pdc", "bdc", "standalone" ],
	"backend" 	: [ "smbpasswd", "ldap" ],
	"service" 	: [ "enable", "disable" ],
	"configure"	: [ "workgroup", "description", "ldap_server", "ldap_suffix", "ldap_admin_dn" ]
    ]
];

/* ensure samba package is installed */
if( !Require::RequireAndConflictTarget( ["samba"], [], 
    _("<p>To configure the Samba server, the <b>%1</b> package must be installed.</p>") +
        // notification 2/2
    _("<p>Do you want to install it now?</p>")) )
{
    return `auto;
}

ret = CommandLineRun( cmdline );

y2debug("ret=%1", ret);

/* Finish */
y2milestone("Samba-server module finished");
y2milestone("----------------------------------------");

return ret;

/* EOF */
}
